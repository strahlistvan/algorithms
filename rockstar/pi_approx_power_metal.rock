Moonlight takes Darkness
  the midnight is fantastical 
  If Darkness is gone
    Give back the midnight
  
  The snow was fantastical
  While the midnight is stronger than Darkness
    Put the snow of Darkness into the snow
    Knock Darkness down
  
Give back the snow

Power takes Light and Fire
  Eternity is fantastical

 Put Eternity into my destiny
 While Fire is higher than nothing
   Put my destiny of Light into my destiny
   Knock Fire down
 
Give back my destiny

The battlecry is everlasting. When I burn in a hot desert beyond my icy heavens, one
Shout The battlecry
The dragon was on
Destiny was eternally reflects courageous o
Put The battlecry times The dragon over Destiny into CONSTANT
(CONSTANT was 0.0002885855652225477)
Say "Constant multiplier"
Say CONSTANT

LIMIT is 2
INDEX is nothing

FOUR is 4
CONST ONE is 1103
CONST TWO is 26390
CONST THREE is 396

SUM is nothing
While INDEX is less than LIMIT
  Put FOUR times INDEX into FOURK
  Put Moonlight taking FOURK into FOURKMoonlight
  Put Moonlight taking INDEX into KMoonlight
  Put CONST ONE plus CONST TWO times INDEX into KCONST
  Put FOURKMoonlight times KCONST into NUMERATOR
  Shout "Numerator"
  Say NUMERATOR
  Put Power taking KMoonlight, FOUR into TAGONE
  Put Power taking CONST THREE, FOURK into TAGTWO
  Put TAGONE times TAGTWO into DENOMINATOR
  Shout "Denominator"  
  Say DENOMINATOR
  Put NUMERATOR over DENOMINATOR into DIFF
  Say DIFF
  Put SUM plus DIFF into SUM
  Build INDEX up

Say INDEX
 
ONE is 1
Put CONSTANT times SUM into PIINV
Shout ONE over PIINV
